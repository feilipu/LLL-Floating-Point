;==================================================================================
; Contents of this file are copyright Phillip Stevens
;
; You have permission to use this for NON COMMERCIAL USE ONLY
; If you wish to use it elsewhere, please include an acknowledgement to myself.
;
;
; https://github.com/feilipu/
;
; https://feilipu.me/
;

;==============================================================================
;
; INCLUDES SECTION
;

;==============================================================================
;
; DEFINES SECTION
;

INT0_APU_ISR    .EQU     $2900 ; start of the APU ISR asm code (RAM)

;==================================================================================
;
; MACRO SECTION
;

; #DEFINE 

;==================================================================================
;
; ORIGIN
;

        .ORG INT0_APU_ISR      ;DRIVER ORIGIN FOR YAZ180 DURING TESTING

;==================================================================================
;
; INTERRUPT SECTION
;
; Interrupt Service Routine for the Am9511A-1
; 
; Initially called once the required operand pointers and commands are loaded
; Following calls generated by END signal whenever a single APU command is completed
; Sends a new command (with operands if needed) to the APU

APU_ISR:
        push af                 ; store AF, etc, so we don't clobber them
        push bc
        push de
        push hl
        
        xor a                   ; Set internal clock = crystal x 1 = 18.432MHz
                                ; That makes the PHI 9.216MHz
        out0 (CMR), a           ; CPU Clock Multiplier Reg (CMR)

APU_ISR_ENTRY:
        ld a, (APUCMDBufUsed)   ; check whether we have a command to do
        or a                    ; zero?
        jr z, APU_ISR_END       ; if so then clean up and END

        ld hl, (APUCMDOutPtr)   ; get the pointer to place where we pop the COMMAND
        ld a, (hl)              ; get the COMMAND byte
        ld (APUStatus), a       ; save the COMMAND (as a status byte)

        ld bc, PIOB             ; 82C55 IO PORT B address in BC XXX
        out (c),a               ; put COMMAND onto Port B

        inc l                   ; move the COMMAND pointer low byte along, 0xFF rollover
        ld (APUCMDOutPtr), hl   ; write where the next byte should be popped

        ld hl, APUCMDBufUsed
        dec (hl)                ; atomically decrement current COMMAND count remaining

        and $F0                 ; mask MSB of COMMAND
        cp APU_OP_ENT           ; check whether it is OPERAND entry COMMAND
        jr z, APU_ISR_OP_ENT    ; load an OPERAND

        cp APU_OP_REM           ; check whether it is OPERAND removal COMMAND
        jr z, APU_ISR_OP_REM    ; remove an OPERAND

        ld a, (APUStatus)       ; recover the COMMAND from Status
        ld bc, APUCNTL          ; the address of the APU control port in BC
        out (c), a              ; load the COMMAND

        in0 a, (ITC)            ; Get INT/TRAP Control Register (ITC)
        or  ITC_ITE0            ; Mask in INT0
        out0 (ITC), a           ; Enable external interrupt INT0

APU_ISR_EXIT:
        pop hl                  ; recover HL, etc
        pop de
        pop bc
        pop af
        ei
        reti

APU_ISR_OP_ENT:      
        ld hl, (APUPTROutPtr)   ; get the pointer to where we pop OPERAND POINTER
        ld e, (hl)              ; read the POINTER low byte from the APUPTROutPtr
        inc l                   ; move the POINTER low byte along, 0xFF rollover
        ld d, (hl)              ; read the POINTER high byte from the APUPTROutPtr
        inc l
        ld (APUPTROutPtr), hl   ; write where the next POINTER should be read
                 
        ld hl, APUPTRBufUsed    ; atomic decrement of POINTER count
        dec (hl)
        dec (hl)

        ld bc, APUDATA+$0300    ; the address of the APU data port in BC
        ex de, hl               ; move the base address of the OPERAND to HL

        outi                    ; output 16 bit OPERAND
        outi

        ld a, (APUStatus)       ; recover the COMMAND status
        cp APU_OP_ENT16         ; is it a 2 byte OPERAND
        jr z, APU_ISR_ENTRY     ; yes? then go back to get another COMMAND

        outi                    ; output last two bytes of 32 bit OPERAND
        outi
        
        jr APU_ISR_ENTRY        ; go back to get another COMMAND

APU_ISR_OP_REM:
        ld hl, (APUPTROutPtr)   ; get the pointer to where we pop OPERAND POINTER
        ld e, (hl)              ; read the POINTER low byte from the APUPTROutPtr
        inc l                   ; move the POINTER low byte along, 0xFF rollover
        ld d, (hl)              ; read the POINTER high byte from the APUPTROutPtr
        inc l
        ld (APUPTROutPtr), hl   ; write where the next POINTER should be read
                 
        ld hl, APUPTRBufUsed    ; atomic decrement of OPERAND POINTER count
        dec (hl)
        dec (hl)

        ld bc, APUDATA+$0300    ; the address of the APU data port in BC
        ex de, hl               ; move the base address of the OPERAND to HL

        inc hl                  ; reverse the OPERAND bytes to load

        ld a, (APUStatus)       ; recover the COMMAND status
        cp APU_OP_REM16         ; is it a 2 byte OPERAND
        jr z, APU_ISR_REM16     ; yes then skip

        inc hl                  ; increment two more bytes for 32bit OPERAND
        inc hl
        ind                     ; get the higher two bytes of 32bit OPERAND
        ind

APU_ISR_REM16:
        ind                     ; get 16 bit OPERAND
        ind                     ; There is only ever one result

APU_ISR_END:                    ; We're done
        in0 a, (ITC)            ; Get INT/TRAP Control Register (ITC)
        and ~ITC_ITE0           ; Mask out INT0   
        out0 (ITC), a           ; Disable external interrupt INT0
        
        xor a
        ld (APUStatus), a       ; Set the status back to idle (NOP)

        ld bc, PIOB             ; 82C55 IO PORT B address in BC XXX
        out (c),a               ; put COMMAND onto Port B

                                ; Set internal clock = crystal x 2 = 36.864MHz
        ld a, CMR_X2            ; Set Hi-Speed flag
        out0 (CMR), a           ; CPU Clock Multiplier Reg (CMR)

        jr APU_ISR_EXIT         ; We're done here

        
;==================================================================================
;
; CODE SECTION
;

;------------------------------------------------------------------------------
;       APU_INIT
;       Initialises the APU buffers

APU_INIT:
        push af
        push bc
        push de
        push hl
  
        LD  HL, APUCMDBuf       ; Initialise COMMAND Buffer
        LD (APUCMDInPtr), HL
        LD (APUCMDOutPtr), HL

        LD HL, APUPTRBuf        ; Initialise OPERAND POINTER Buffer
        LD (APUPTRInPtr), HL
        LD (APUPTROutPtr), HL

        XOR A                   ; clear A regiser to 0

        LD (APUCMDBufUsed), A   ; 0 both Buffer counts
        LD (APUPTRBufUsed), A

        LD (APUCMDBuf), A       ; Clear COMMAND Buffer
        LD HL, APUCMDBuf
        LD D, H
        LD E, L
        INC DE
        LD BC, APU_CMD_BUFSIZE+1
        LDIR

        LD (APUPTRBuf), A       ; Clear OPERAND POINTER Buffer
        LD HL, APUPTRBuf
        LD D, H
        LD E, L
        INC DE
        LD BC, APU_PTR_BUFSIZE+1
        LDIR

        LD (APUStatus), A       ; Set APU status to idle (NOP)
        
        ld hl, INT0_APU_ISR     ; load our origin into the jump table 
                                ; initially there is a RETI there
        ld (INT_INT0_ADDR), hl  ; load the address of the APU INT0 jump
        
APU_INIT_LOOP:
        ld bc, APUCNTL          ; the address of the APU Control port in bc
        in a, (c)               ; read the APU
        and $80                 ; Busy?
        jr nz, APU_INIT_LOOP

        pop hl
        pop de
        pop bc
        pop af
        ret

;------------------------------------------------------------------------------
;       APU_CHK_IDLE
;       Confirms whether the APU is idle
;       Loop until it returns ready
;       Operand Entry and Removal takes little time,
;       and we'll be interrupted for Command entry.

APU_CHK_IDLE:
        push af
        push bc

APU_CHK_LOOP:
        ld a, (APUCMDBufUsed)   ; Get the number of bytes in the COMMAND buffer

        ld bc, PIOB             ; 82C55 IO PORT B address in BC XXX
        out (c),a               ; put COMMANDS remaining onto Port B

        or a                    ; check whether COMMAND buffer is empty
        jr nz, APU_CHK_LOOP     ; COMMAND buffer not empty, so wait

        ld a, (APUStatus)       ; get the status of the APU
        or a                    ; check it is zero (NOP)
        jr nz, APU_CHK_LOOP     ; otherwise wait

        ld bc, APUCNTL          ; the address of the APU control port in bc
        in a, (c)               ; read the APU
        and $80                 ; Busy?
        jr nz, APU_CHK_LOOP     ; then wait

        pop bc
        pop af
        ret

;------------------------------------------------------------------------------
;       APU_OP_LD
;       POINTER to OPERAND in DE
;       APU COMMAND in A

APU_OP_LD:
        push hl                 ; store HL so we don't clobber it
        ld l, a                 ; store COMMAND so we don't clobber it

        ld a, (APUCMDBufUsed)   ; Get the number of bytes in the COMMAND buffer
        cp APU_CMD_BUFSIZE      ; check whether there is space in the buffer
        jr nc, APU_OP_EXIT      ; COMMAND buffer full, so exit

        ld a, l                 ; recover the operand entry COMMAND
        ld hl, (APUCMDInPtr)    ; get the pointer to where we poke
        ld (hl), a              ; write the COMMAND byte to the APUCMDInPtr   

        inc l                   ; move the COMMAND pointer low byte along, 0xFF rollover
        ld (APUCMDInPtr), hl    ; write where the next byte should be poked

        ld hl, APUCMDBufUsed
        inc (hl)                ; atomic increment of COMMAND count

        ld a, (APUPTRBufUsed)   ; Get the number of bytes in the OPERAND POINTER buffer
        cp APU_PTR_BUFSIZE-1    ; check whether there is space for a POINTER
        jr nc, APU_OP_EXIT       ; buffer full, so exit
        
        ld hl, (APUPTRInPtr)    ; get the pointer to where we poke
        ld (hl), e              ; write the low byte of POINTER to the APUPTRInPtr   
        inc l                   ; move the POINTER low byte along, 0xFF rollover
        ld (hl), d              ; write the high byte of POINTER to the APUPTRInPtr   
        inc l
        ld (APUPTRInPtr), hl    ; write where the next POINTER should be poked

        ld hl, APUPTRBufUsed
        inc (hl)
        inc (hl)

APU_OP_EXIT:
        pop hl                  ; recover HL      
        ret

;------------------------------------------------------------------------------
;       APU_CMD_LD
;       APU COMMAND in A

APU_CMD_LD:
        push hl                 ; store HL so we don't clobber it
        ld l, a                 ; store COMMAND so we don't clobber it

        ld a, (APUCMDBufUsed)   ; Get the number of bytes in the COMMAND buffer
        cp APU_CMD_BUFSIZE      ; check whether there is space in the buffer
        jr nc, APU_CMD_EXIT     ; COMMAND buffer full, so exit

        ld a, l                 ; recover the operand entry COMMAND
        ld hl, (APUCMDInPtr)    ; get the pointer to where we poke
        ld (hl), a              ; write the COMMAND byte to the APUCMDInPtr   

        inc l                   ; move the COMMAND pointer low byte along, 0xFF rollover
        ld (APUCMDInPtr), hl    ; write where the next byte should be poked

        ld hl, APUCMDBufUsed
        inc (hl)                ; atomic increment of COMMAND count

APU_CMD_EXIT:
        pop hl                  ; recover HL
        ret

;==============================================================================
;
        .END
;
;==============================================================================

